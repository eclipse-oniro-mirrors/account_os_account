# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../../os_account.gni")

config("appaccount_kit_config") {
  include_dirs = [
    "${app_account_interfaces_native_path}/include",
    "${app_account_kits_path}/include",
    "${os_account_path}/interfaces/kits/napi/common/include",
    "include",
    "${app_account_innerkits_native_path}/include",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  cflags += [ "-pipe" ]

  defines = [
    "ACCOUNT_LOG_TAG = \"AppAccountJsKit\"",
    "LOG_DOMAIN = 0xD001B00",
  ]
}

ohos_shared_library("appaccount") {
  include_dirs = []
  if (!use_clang_coverage) {
    version_script = "libappaccount.map"
  }
  configs = [
    ":appaccount_kit_config",
    "${account_coverage_config_path}:coverage_flags",
  ]

  public_configs = [ ":appaccount_kit_config" ]
  sources = [
    "src/napi_app_account.cpp",
    "src/napi_app_account_authenticator.cpp",
    "src/napi_app_account_authenticator_callback.cpp",
    "src/napi_app_account_common.cpp",
    "src/napi_app_account_constant.cpp",
    "src/napi_app_account_module.cpp",
  ]

  deps = [
    "${account_napi_common_path}:account_napi_common",
    "${app_account_innerkits_native_path}:app_account_innerkits",
    "${common_path}:libaccount_common",
  ]

  relative_install_dir = "module/account"

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:abilitykit_native",
    "ability_runtime:app_context",
    "ability_runtime:extensionkit_native",
    "ability_runtime:napi_base_context",
    "ability_runtime:runtime",
    "ability_runtime:service_extension",
    "ability_runtime:ui_extension",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_napi",
    "ipc:ipc_single",
    "napi:ace_napi",
  ]

  subsystem_name = "account"
  part_name = "os_account"
}
