# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../../os_account.gni")

config("account_iam_kit_config") {
  include_dirs = [ "include" ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  cflags += [ "-pipe" ]
  cflags_cc = [
    "-Wdate-time",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wshadow",
  ]
}

ohos_shared_library("account_iam_napi") {
  configs = [
    ":account_iam_kit_config",
    "${account_coverage_config_path}:coverage_flags",
  ]
  public_configs = [ ":account_iam_kit_config" ]
  sources = [
    "${account_iam_kits_path}/src/napi_account_iam_common.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_constant.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_identity_manager.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_module.cpp",
    "${account_iam_kits_path}/src/napi_account_iam_user_auth.cpp",
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"AccountIAM\"",
    "LOG_DOMAIN = 0xD001B00",
  ]

  deps = [
    "${account_iam_framework_path}:account_iam_innerkits",
    "${account_napi_common_path}:account_napi_common",
  ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  cflags_cc = []
  if (has_pin_auth_part) {
    cflags_cc += [ "-DHAS_PIN_AUTH_PART" ]
    sources += [
      "${account_iam_kits_path}/src/napi_account_iam_inputer_manager.cpp",
      "${account_iam_kits_path}/src/napi_account_iam_pin_auth.cpp",
    ]
    external_deps += [ "pin_auth:pinauth_framework" ]
  }
  if (has_user_auth_part) {
    cflags_cc += [ "-DHAS_USER_AUTH_PART" ]
    external_deps += [ "user_auth_framework:userauth_client" ]
  }

  subsystem_name = "account"
  innerapi_tags = [ "platformsdk" ]
  part_name = "os_account"
}
