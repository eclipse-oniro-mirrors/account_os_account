# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../os_account.gni")

config("public_account_config") {
  include_dirs = [
    "include",
    "${common_path}/include",
    "${common_path}/utils/include",
    "${innerkits_common}/include",
    "${innerkits_path}/include",
  ]
}

config("accountkits_config") {
  defines = [
    "ACCOUNT_LOG_TAG = \"DistributedAccountFwk\"",
    "LOG_DOMAIN = 0xD001B00",
  ]
}

ohos_shared_library("libaccountkits") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = [
    "src/account_info_parcel.cpp",
    "src/account_proxy.cpp",
    "src/distributed_account_event_proxy.cpp",
    "src/distributed_account_event_service.cpp",
    "src/distributed_account_event_stub.cpp",
    "src/distributed_account_subscribe_callback.cpp",
    "src/ohos_account_kits.cpp",
    "src/ohos_account_kits_impl.cpp",
    "src/system_ability_status_change_listener.cpp",
  ]

  cflags = [ "-pipe" ]
  cflags_cc = [
    "-Wdate-time",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wshadow",
  ]

  configs = [
    ":accountkits_config",
    "${account_coverage_config_path}:coverage_flags",
  ]

  public_configs = [ ":public_account_config" ]

  if (!use_clang_coverage) {
    version_script = "libohos_account_innerkits.map"
  }

  deps = [ "${common_path}:libaccount_common" ]

  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "samgr:samgr_proxy",
  ]
  public_external_deps = [ "json:nlohmann_json_static" ]
  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "account"
  part_name = "os_account"
}
