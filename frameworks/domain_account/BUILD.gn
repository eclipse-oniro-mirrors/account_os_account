# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../os_account.gni")

config("domain_account_innnerkits_config") {
  visibility = [ ":*" ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }

  cflags += [ "-pipe" ]
  cflags_cc = [
    "-Wdate-time",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wshadow",
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"DomainAccountFwk\"",
    "LOG_DOMAIN = 0xD001B00",
  ]
}

config("domain_account_innerkits_public_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "${common_path}/include",
    "${domain_account_interfaces_native_path}/include",
    "${common_path}/utils/include",
    "${innerkits_common}/include",
  ]
}

ohos_shared_library("domain_account_innerkits") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = [
    "src/domain_account_callback_adapters.cpp",
    "src/domain_account_callback_client.cpp",
    "src/domain_account_callback_proxy.cpp",
    "src/domain_account_callback_service.cpp",
    "src/domain_account_callback_stub.cpp",
    "src/domain_account_client.cpp",
    "src/domain_account_common.cpp",
    "src/domain_account_plugin_service.cpp",
    "src/domain_account_plugin_stub.cpp",
    "src/domain_account_proxy.cpp",
    "src/domain_account_status_listener_manager.cpp",
  ]

  configs = [ ":domain_account_innnerkits_config" ]

  public_configs = [ ":domain_account_innerkits_public_config" ]

  deps = [
    "${common_path}:libaccount_common",
    "${innerkits_native_path}:libaccountkits",
  ]

  external_deps = [
    "ability_base:want",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  if (ohos_indep_compiler_enable) {
    external_deps += [ "samgr:samgr_proxy" ]
  }

  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "account"
  part_name = "os_account"
}
