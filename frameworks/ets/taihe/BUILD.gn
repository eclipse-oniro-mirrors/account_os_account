# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/account/os_account/os_account.gni")
import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")

copy_taihe_idl("copy_account") {
  sources = [
    "app_account/idl/ohos.account.appAccount.taihe",
    "distributed_account/idl/ohos.account.distributedAccount.taihe",
    "os_account/idl/ohos.account.osAccount.taihe",
  ]
}

subsystem_name = "account"
part_name = "os_account"
taihe_generated_file_path_app_account = "$taihe_file_path/out/$subsystem_name/$part_name/app_account"
taihe_generated_file_path_distributed_account = "$taihe_file_path/out/$subsystem_name/$part_name/distributed_account"
taihe_generated_file_path_osaccount = "$taihe_file_path/out/$subsystem_name/$part_name/osaccount"

ohos_taihe("app_account_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_app_account}"
  deps = [ ":copy_account" ]
  outputs = [
    "${taihe_generated_file_path}/src/ohos.account.appAccount.ani.cpp",
    "${taihe_generated_file_path}/src/ohos.account.appAccount.abi.c",
  ]
}

taihe_shared_library("app_account_taihe_native") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  taihe_generated_file_path = "${taihe_generated_file_path_app_account}"
  part_name = "${part_name}"
  subsystem_name = "${subsystem_name}"

  sources = get_target_outputs(":app_account_taihe")
  sources += [
    "./app_account/src/ani_constructor.cpp",
    "./app_account/src/ohos.account.appAccount.impl.cpp",
  ]

  include_dirs = [
    "./app_account/include",
    "${os_account_path}/frameworks/appaccount/native/include",
    "${os_account_path}/interfaces/innerkits/appaccount/native/include",
    "${os_account_path}/frameworks/ets/taihe/os_account/include/",
    "${taihe_generated_file_path}/include",
    "${innerkits_path}/include",
    "${common_path}/account_error",
    "${common_path}/log/include",
  ]

  deps = [
    ":app_account_taihe",
    "${account_napi_common_path}:account_napi_common",
    "${app_account_innerkits_native_path}:app_account_innerkits",
    "${innerkits_native_path}:libaccountkits",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ani_common",
    "ability_runtime:app_context",
    "ability_runtime:runtime",
    "c_utils:utils",
    "hilog:libhilog",
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"TH_APP_Account\"",
    "LOG_DOMAIN = 0xD001B00",
  ]
}

ohos_taihe("distributed_account_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_distributed_account}"
  deps = [ ":copy_account" ]
  outputs = [
    "${taihe_generated_file_path}/src/ohos.account.distributedAccount.ani.cpp",
    "${taihe_generated_file_path}/src/ohos.account.distributedAccount.abi.c",
  ]
}

taihe_shared_library("distributed_account_taihe_native") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  taihe_generated_file_path = "${taihe_generated_file_path_distributed_account}"
  part_name = "${part_name}"
  subsystem_name = "${subsystem_name}"

  sources = get_target_outputs(":distributed_account_taihe")
  sources += [
    "./common/src/taihe_distributed_account_converter.cpp",
    "./distributed_account/src/ani_constructor.cpp",
    "./distributed_account/src/ohos.account.distributedAccount.impl.cpp",
  ]

  include_dirs = [
    "./distributed_account/include",
    "./common/include",
    "${taihe_generated_file_path}/include",
    "${innerkits_path}/include",
    "${common_path}/account_error",
    "${common_path}/log/include",
  ]

  deps = [
    ":distributed_account_taihe",
    "${account_napi_common_path}:account_napi_common",
    "${innerkits_native_path}:libaccountkits",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ani_common",
    "ability_runtime:app_context",
    "ability_runtime:runtime",
    "c_utils:utils",
    "hilog:libhilog",
    "napi:ace_napi",
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"TH_OHOS_Account\"",
    "LOG_DOMAIN = 0xD001B00",
  ]
}

ohos_taihe("osaccount_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_osaccount}"
  deps = [ ":copy_account" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.account.osAccount.ani.cpp",
    "$taihe_generated_file_path/src/ohos.account.osAccount.abi.c",
  ]
}

taihe_shared_library("os_account_taihe_native") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "${taihe_generated_file_path_osaccount}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"

  sources = get_target_outputs(":osaccount_taihe")
  sources += [
    "./common/src/taihe_distributed_account_converter.cpp",
    "os_account/src/ani_constructor.cpp",
    "os_account/src/ohos.account.osAccount.impl.cpp",
    "os_account/src/taihe_account_info.cpp",
    "os_account/src/taihe_common.cpp",
  ]

  include_dirs = [
    "os_account/include",
    "distributed_account/include",
    "./common/include",
    "${common_path}/log/include",
    "${taihe_generated_file_path}/include",
    "${innerkits_path}/include",
    "${common_path}/account_error",
    "${os_account_interfaces_native_path}/include",
    "${account_iam_interfaces_native_path}/include",
    "${account_iam_kits_path}/include",
  ]

  deps = [
    ":distributed_account_taihe",
    ":osaccount_taihe",
    "${account_iam_framework_path}:account_iam_innerkits",
    "${account_napi_common_path}:account_napi_common",
    "${common_path}:libaccount_common",
    "${domain_account_framework_path}:domain_account_innerkits",
    "${innerkits_native_path}:libaccountkits",
    "${os_account_innerkits_native_path}:os_account_innerkits",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ani_common",
    "ability_runtime:app_context",
    "ability_runtime:runtime",
    "access_token:libtokenid_sdk",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
    "napi:ace_napi",
  ]

  cflags_cc = [ "-w" ]

  if (has_user_auth_part) {
    cflags_cc += [ "-DHAS_USER_AUTH_PART" ]
    external_deps += [ "user_auth_framework:userauth_client" ]
  }

  if (has_pin_auth_part) {
    cflags_cc += [ "-DHAS_PIN_AUTH_PART" ]
    external_deps += [ "pin_auth:pinauth_framework" ]
  }

  defines = [
    "ACCOUNT_LOG_TAG = \"TH_OS_Account\"",
    "LOG_DOMAIN = 0xD001B00",
  ]
}

generate_static_abc("distributed_account_abc") {
  base_url = "${taihe_generated_file_path_distributed_account}"
  files =
      [ "${taihe_generated_file_path_distributed_account}/@ohos.account.distributedAccount.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/distributed_account_abc.abc"
  dependencies = [ ":distributed_account_taihe" ]
}

generate_static_abc("app_account_abc") {
  base_url = "${taihe_generated_file_path_app_account}"
  files = [ "${taihe_generated_file_path_app_account}/@ohos.account.appAccount.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/app_account_abc.abc"
  dependencies = [ ":app_account_taihe" ]
}

generate_static_abc("os_account_abc") {
  base_url = "${taihe_generated_file_path_osaccount}"
  files = [ "${taihe_generated_file_path_osaccount}/@ohos.account.osAccount.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/os_account_abc.abc"
  dependencies = [ ":osaccount_taihe" ]
}

generate_static_abc("os_account_abc_test") {
  base_url = "${os_account_path}/frameworks/ets/taihe/test"
  files = [
    "${os_account_path}/frameworks/ets/taihe/test/test_main.ets",
    "${os_account_path}/frameworks/ets/taihe/test/test_accountmgr.ets",
    "${os_account_path}/frameworks/ets/taihe/test/test_distributed_account.ets",
    "${os_account_path}/frameworks/ets/taihe/test/test_iam.ets",
    "${os_account_path}/frameworks/ets/taihe/test/test_common.ets",
  ]
  dependencies = [
    ":os_account_abc",
    ":distributed_account_abc",
  ]
}

ohos_prebuilt_etc("distributed_account_etc") {
  source = "${target_out_dir}/distributed_account_abc.abc"
  module_install_dir = "framework"
  part_name = "${part_name}"
  subsystem_name = "${subsystem_name}"
  deps = [ ":distributed_account_abc" ]
}

ohos_prebuilt_etc("os_account_etc") {
  source = "$target_out_dir/os_account_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":os_account_abc" ]
}

group("distributed_account_taihe_gp") {
  deps = [
    ":distributed_account_etc",
    ":distributed_account_taihe_native",
  ]
}

group("os_account_taihe_gp") {
  deps = [
    # ":os_account_abc_test",
    ":os_account_etc",
    ":os_account_taihe_native",
  ]
}

group("account_taihe") {
  deps = [
    ":distributed_account_taihe_gp",
    ":os_account_taihe_gp",
  ]
}
