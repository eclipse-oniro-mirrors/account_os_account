# Copyright (c) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../os_account.gni")

group("common_target") {
  deps = [ ":libaccount_common" ]
}

config("account_common_config") {
  include_dirs = [
    "include",
    "file_operator/include",
    "log/include",
    "perf_stat/include",
    "${os_account_dfx_path}/hisysevent_adapter",
    "${os_account_dfx_path}/data_dfx",
    "utils/include",
    "${innerkits_common}/include",
    "${innerkits_path}/include",
    "json_utils/include",
    "${os_account_path}/interfaces/innerkits/osaccount/native/include",
    "${domain_account_interfaces_native_path}/include",
    "${app_account_innerkits_native_path}/include",
  ]
}

ohos_shared_library("libaccount_common") {
  branch_protector_ret = "pac_ret"

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  sources = [
    "${app_account_innerkits_native_path}/src/app_account_info.cpp",
    "${innerkits_native_path}/src/distributed_account_subscribe_callback.cpp",
    "${os_account_dfx_path}/data_dfx/data_size_report_adapter.cpp",
    "${os_account_dfx_path}/hisysevent_adapter/account_hisysevent_adapter.cpp",
    "account_error/src/account_error_no.cpp",
    "file_operator/src/account_file_operator.cpp",
    "json_utils/src/app_account_info_json_parser.cpp",
    "json_utils/src/json_utils.cpp",
    "json_utils/src/os_account_info_json_parser.cpp",
    "log/src/account_log_wrapper.cpp",
    "perf_stat/src/perf_stat.cpp",
    "utils/src/account_permission_manager.cpp",
    "utils/src/account_timeout_task.cpp",
    "utils/src/memory_guard.cpp",
  ]

  cflags = [ "-pipe" ]
  cflags_cc = [
    "-Wdate-time",
    "-Wformat=2",
    "-Wfloat-equal",
    "-Wshadow",
  ]

  defines = [
    "ACCOUNT_LOG_TAG = \"AccountCommonFwk\"",
    "LOG_DOMAIN = 0xD001B00",
  ]

  if (use_musl) {
    if (musl_use_jemalloc && musl_use_jemalloc_dfx_intf) {
      defines += [ "CONFIG_USE_JEMALLOC_DFX_INTF" ]
    }
  }

  public_configs = [ ":account_common_config" ]

  external_deps = [
    "ability_base:want",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "cJSON:cjson",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  if (hicollie_enable == true) {
    sources += [ "utils/src/account_timer.cpp" ]
    external_deps += [ "hicollie:libhicollie" ]
    cflags_cc += [ "-DHICOLLIE_ENABLE" ]
  }

  if (os_account_distributed_feature) {
    cflags_cc += [ "-DENABLE_FILE_WATCHER" ]
  }

  if (has_hiviewdfx_hitrace_part) {
    cflags_cc += [ "-DHAS_HISYSEVENT_PART" ]
    external_deps += [ "hisysevent:libhisysevent" ]
  }

  subsystem_name = "account"
  innerapi_tags = [ "platformsdk" ]
  part_name = "os_account"
}
